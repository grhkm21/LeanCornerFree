import Mathlib.Analysis.SpecialFunctions.Pow.Real

set_option autoImplicit false

open Nat hiding log sqrt
open Real Filter Asymptotics Topology

example (f g : â„• â†’ â„) (hf : f =o[atTop] (1 : â„• â†’ â„)) (hg : g =O[atTop] f) :
    g =o[atTop] (1 : â„• â†’ â„) := by
  exact hg.trans_isLittleO hf

lemma af : (fun x â†¦ x + 1 : â„• â†’ â„) =O[atTop] (exp Â· : â„• â†’ â„) := by
  apply isBigO_of_le
  intro x
  iterate 2 rw [norm_eq_abs, abs_eq_self.mpr]
  Â· exact add_one_le_exp _
  Â· exact exp_nonneg â†‘x
  Â· linarith

example (f : â„• â†’ â„) (hf : f =o[atTop] (1 : â„• â†’ â„)) :
    (fun x â†¦ sqrt (1 / (1 + f x) - 1)) =o[atTop] (1 : â„• â†’ â„) := by
  apply (isLittleO_one_iff _).mpr
  replace hf := (isLittleO_one_iff _).mp hf
  simpa using (((hf.const_add 1).invâ‚€ (by simp)).sub_const 1).sqrt

example (f : â„• â†’ â„) (hf : Tendsto f atTop (ğ“ 0)) : Tendsto (1 / (1 + f)) atTop (ğ“ 1) := by
  simpa using (hf.const_add 1).invâ‚€

example (f : â„• â†’ â„) (hf : Tendsto f atTop (ğ“ 0)) : Tendsto (fun x â†¦ sqrt (1 / (1 + f x))) atTop (ğ“ 1) := by
  simpa using ((hf.const_add 1).invâ‚€ (by simp)).sqrt

theorem asympt2 {c : â„} (hc : 1 < c) :
    âˆ€ f : â„• â†’ â„, f =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) â†’
      âˆƒ g : â„• â†’ â„, g =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) âˆ§
        (fun N : â„• â†¦ sqrt (log N / log (c + f N)))
          =á¶ [atTop] (fun N â†¦ (1 + g N) * sqrt (log N / log c)) := by
  intro f hf

  /- Step 1: Rewrite log (c + o(1)) to log(c) + log(1 + o(1)) -/
  have step : âˆƒ g : â„• â†’ â„, g =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) âˆ§
      (fun N : â„• â†¦ sqrt (log N / log (c + f N)))
        =á¶ [atTop] (fun N : â„• â†¦ sqrt (log N / (log c + log (1 + g N)))) := by
    use fun N â†¦ f N / c
    constructor
    Â· rw [isLittleO_one_iff] at hf âŠ¢
      simpa using hf.div_const c
    Â· /- Since f â†’ 0, âˆƒ k s.t. N â‰¥ k â†’ 1 + f N / c > 0 -/
      rw [isLittleO_one_iff, atTop_basis.tendsto_iff (nhds_basis_Ioo_pos _)] at hf
      obtain âŸ¨N, _, hNâŸ© := hf 1 zero_lt_one
      simp_rw [Set.mem_Ici, Set.mem_Ioo, zero_sub, zero_add] at hN
      /- Then we can write log(c + f N) = log(c) + log(1 + f N / c) -/
      rw [EventuallyEq, eventually_atTop]
      use N, fun x hx â†¦ ?_
      rw [â† log_mul (by linarith), mul_add, mul_div_cancel' _ (by linarith), mul_one]
      have : -1 < -1 / c := by
        rwa [neg_div, neg_lt_neg_iff, div_lt_iff $ zero_lt_one.trans hc, one_mul]
      have : -1 / c < f x / c := (div_lt_div_right $ zero_lt_one.trans hc).mpr (hN x hx).left
      linarith

  /- Step 2: Rewrite log (1 + o(1)) to o(1) -/
  replace step : âˆƒ g : â„• â†’ â„, g =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) âˆ§
      (fun N : â„• â†¦ sqrt (log N / log (c + f N)))
        =á¶ [atTop] (fun N : â„• â†¦ sqrt (log N / (log c + g N))) := by
    obtain âŸ¨g, hg, hg'âŸ© := step
    use fun N â†¦ log (1 + g N), ?_, hg'
    rw [isLittleO_one_iff] at hg âŠ¢
    have : ContinuousAt log 1 := continuousAt_log one_ne_zero
    convert Tendsto.comp (continuousAt_log one_ne_zero) (by simpa using hg.const_add 1)
    exact log_one.symm

  /- Step 3: Isolate asymptotic term from constant factor -/
  replace step : âˆƒ g : â„• â†’ â„, g =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) âˆ§
      (fun N : â„• â†¦ sqrt (log N / log (c + f N)))
        =á¶ [atTop] (fun N : â„• â†¦ sqrt (1 / (1 + g N)) * sqrt (log N / log c)) := by
    obtain âŸ¨g, hg, hg'âŸ© := step
    use fun N â†¦ g N / log c, ?_
    Â· rw [EventuallyEq, eventually_atTop] at hg' âŠ¢
      obtain âŸ¨N, hNâŸ© := hg'
      use max N 1, fun b hb â†¦ ?_
      rw [mul_comm, â† sqrt_mul, mul_one_div, div_div, mul_add, mul_one, mul_div_cancel']
      Â· exact hN b (le_of_max_le_left hb)
      Â· apply log_eq_zero.not.mpr
        push_neg
        use ?_, ?_, ?_ <;> linarith [hc]
      Â· apply div_nonneg
        Â· have hb' := le_of_max_le_right hb
          refine (log_nonneg_iff ?_).mpr (by simpa using hb')
          simpa only [cast_pos] using hb'
        Â· exact (log_nonneg_iff (by linarith)).mpr hc.le
    Â· rw [isLittleO_one_iff] at hg âŠ¢
      simpa using hg.div_const (log c)

  /- Step 4: Rewrite sqrt (1 / (1 + o(1))) as (1 + o(1)) -/
  replace step : âˆƒ g : â„• â†’ â„, g =o[atTop] (fun _ â†¦ 1 : â„• â†’ â„) âˆ§
      (fun N : â„• â†¦ sqrt (log N / log (c + f N)))
        =á¶ [atTop] (fun N : â„• â†¦ (1 + g N) * sqrt (log N / log c)) := by
    obtain âŸ¨g, hg, hg'âŸ© := step
    use fun N â†¦ sqrt (1 / (1 + g N)) - 1, ?_
    Â· rw [EventuallyEq, eventually_atTop] at hg' âŠ¢
      obtain âŸ¨N, hNâŸ© := hg'
      simp_rw [add_sub_cancel'_right]
      exact âŸ¨N, hNâŸ©
    Â· rw [isLittleO_one_iff] at hg âŠ¢
      simpa using ((hg.const_add 1).invâ‚€ (by linarith)).sqrt.sub_const 1

  exact step
